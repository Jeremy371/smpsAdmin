buildscript {
    ext {
        springBootVersion = '1.3.4.RELEASE'
        retrofitVersion = '2.0.2'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.1'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'edu.sc.seis.launch4j'

war {
    baseName = 'smpsAdmin'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://jasperreports.sourceforge.net/maven2/' }
    maven { url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/' }
    maven { url 'http://humane.ipdisk.co.kr:7000/content/repositories/releases/' }
}

configurations {
    providedRuntime
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.projectlombok:lombok:1.16.8')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-tomcat')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    compile "org.springframework.boot:spring-boot-configuration-processor"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'

    compile 'io.reactivex:rxjava:1.1.3'
    compile('com.google.code.gson:gson:2.6.2')

    compile('net.sf.jasperreports:jasperreports:6.2.1')
    compile('org.apache.poi:poi:3.14')
    compile('com.lowagie:itextAsian:2.1.7')
    compile('net.sourceforge.barbecue:barbecue:1.5-beta1')

    compile('org.apache.commons:commons-lang3:3.4')
    compile('commons-io:commons-io:2.4')
    compile('com.humane:util-common:0.0.14')
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

bootRun {
    addResources = true
    systemProperties System.properties
}

launch4j {
    mainClassName = 'com.humane.admin.smps.Application'
    headerType = 'console'
}